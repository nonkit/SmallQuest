'Small Quest
'Version 0.6.01a
'Copyright © 2015 Filipe Cardoso Martins and Nonki Takahashi.  The MIT License.
'Program ID HDL692-3
'Last update 2015-06-25

Not = "False=True;True=False;"
CRLF = Text.GetCharacter(13) + Text.GetCharacter(10)

'Definições de idioma. / Language settings.
lang = "en-US"
Lang_GetTextFromDAT()

'Configurações da janela do jogo. / The game window settings.
GraphicsWindow.Hide()
GraphicsWindow.Show()
GraphicsWindow.Title = "Small Quest"
GraphicsWindow.Height = "550" 
GraphicsWindow.Width = "800"

Game_Frame()
Game_Characters()
Game_Actions()
Game_Start()
Program.End()

Sub Game_Frame
  'Designer da janela do jogo. / Designer of the game window.
  GraphicsWindow.BackgroundColor= "DodgerBlue"
  GraphicsWindow.BrushColor= GraphicsWindow.GetColorFromRGB (255, 255, 255)
  GraphicsWindow.FillRectangle (10, 10, 780, 470)
  GraphicsWindow.BrushColor= GraphicsWindow.GetColorFromRGB (255, 255, 255)
  GraphicsWindow.FillRectangle (10, 490, 780, 50)
  GraphicsWindow.BrushColor= GraphicsWindow.GetColorFromRGB (0, 0, 0)
  GraphicsWindow.FillRectangle (13, 493, 774, 44)
  GraphicsWindow.BrushColor= GraphicsWindow.GetColorFromRGB (0, 0, 0)
  GraphicsWindow.FillRectangle (15, 15, 185, 460)
  GraphicsWindow.BrushColor= GraphicsWindow.GetColorFromRGB (0, 0, 0)
  GraphicsWindow.FillRectangle (210, 15, 185, 460)
  GraphicsWindow.BrushColor= GraphicsWindow.GetColorFromRGB (0, 0, 0)
  GraphicsWindow.FillRectangle (405, 15, 185, 460)
  GraphicsWindow.BrushColor= GraphicsWindow.GetColorFromRGB (0, 0, 0)
  GraphicsWindow.FillRectangle (600, 15, 185, 460)
EndSub

Sub Game_Characters
  'Mostrar caracteres. / Show characters.
  GraphicsWindow.FontName = "Trebuchet MS"
  GraphicsWindow.FontSize = 30
  dark = 30
  y1 = 15
  y2 = y1 + 460
  For i = 1 To 4
    x1[i] = 30 + (i - 1) * 195
    x2[i] = x1[i] + 185
    GraphicsWindow.BrushColor = "White"
    name[i] = Shapes.AddText(dat["name" + i])
    Shapes.Move(name[i], 30 + (i - 1) * 195, 496)
    GraphicsWindow.BrushColor = "Gray"
    path = Program.Directory + "\image\" + fname[i] + ".png"
    face[i] = Shapes.AddImage(path)
    Shapes.Move(face[i], 30 + (i - 1) * 195, 150)
    If i = 1 Then
      hover[i] = "True"
    Else
      hover[i] = "False"
      Shapes.SetOpacity(name[i], dark)
      Shapes.SetOpacity(face[i], dark)
    EndIf
  EndFor
EndSub

Sub Game_Actions
  'Mostrar os menus de ação. / Show action menus.
  y = 360
  height = 100
  width = 155
  dark = 30
  GraphicsWindow.FontSize = 16
  nAction = 0
  While dat["action" + (nAction + 1)] <> ""
    nAction = nAction + 1
  EndWhile
  For i = 1 To 4
    x = 30 + (i - 1) * 195
    GraphicsWindow.BrushColor = "#333333"
    action[i] = Shapes.AddRectangle(width, height)
    Shapes.Move(action[i], x, y)
    Shapes.SetOpacity(action[i], dark)
    GraphicsWindow.BrushColor = "#FFFFFF"
    xa = x + 10 
    ya = y + 4
    For j = 1 To nAction
      item[fname[i] + j] = Shapes.AddText(dat["action" + j])
      Shapes.Move(item[fname[i] + j], xa, ya)
      Shapes.SetOpacity(item[fname[i] + j], dark)
      ya = ya + 20
    EndFor
  EndFor
EndSub

Sub Game_Start
  'Começar o jogo. / Start game.
  mouseMove = "False"
  GraphicsWindow.MouseMove = OnMouseMove
  mouseDown = "False"
  GraphicsWindow.MouseDown = OnMouseDown
  selecting = "True"
  While selecting
    If mouseMove Then
      mx = GraphicsWindow.MouseX
      my = GraphicsWindow.MouseY
      selected = ""
      For i = 1 To 4
        If x1[i] <= mx And mx <= x2[i] And y1 <= my And my <= y2 Then
          If hover[i] Then
            For j = 1 To nAction
              ya1 = 364 + (j - 1) * 20
              ya2 = ya1 + 20
              If ya1 <= my And my < ya2 Then
                Shapes.SetOpacity(item[fname[i] + j], 100)
                selected = fname[i] + j
              Else
                Shapes.SetOpacity(item[fname[i] + j], dark)
              EndIf
            EndFor
          Else
            hover[i] = "True"
            Shapes.SetOpacity(name[i], 100)
            Shapes.SetOpacity(face[i], 100)
            Shapes.SetOpacity(action[i], 100)
          EndIf
        Else
          If hover[i] Then
            hover[i] = "False"
            Shapes.SetOpacity(name[i], dark)
            Shapes.SetOpacity(face[i], dark)
            Shapes.SetOpacity(action[i], dark)
            For j = 1 To nAction
              Shapes.SetOpacity(item[fname[i] + j], dark)
            EndFor
          EndIf
        EndIf
      EndFor
      mouseMove = "False"
    Else
      Program.Delay(200)
    EndIf
    If mouseDown Then
      Game_HideBalloon()
      If Text.EndsWith(selected, 1) Then      'Converse / Talk
        For i = 1 To 4
          If Text.StartsWith(selected, fname[i]) Then
            character = i
            i = 4
          EndIf
        EndFor
        i = character
        If i = 1 Then
        txt = "Hi everyone.  Today, I have a crazy suggestion for us."
        big = "True"
        Else
        txt = "What do you" + CRLF + "mean?"
        big = "False"
        EndIf
        Game_ShowBalloon()
        mouseDown = "False"
      ElseIf Text.EndsWith(selected, 2) Then  'Dorme / Sleep
        selecting = "False"
      EndIf
      mouseDown = "False"
    EndIf
  EndWhile
EndSub

Sub Game_ShowBalloon
  'Desenhe balão. / Draw balloon.
  'param i - index 1..4 for each character
  'param big - "True" for big balloon, "False" for small balloon
  'param txt - balloon text
  r = 20
  height = 110
  yb = y1 + 10
  dx = (i - 1) * 195
  GraphicsWindow.PenWidth = 0
  GraphicsWindow.BrushColor = "#FFFFFF"
  If big Then
    width = 740
    xb = 30
  Else
    width = 155
    xb = 30 + dx
  EndIf
  balloon[1] = Shapes.AddRectangle(width, height - 2 * r)
  Shapes.Move(balloon[1], xb, yb + r)
  balloon[2] = Shapes.AddRectangle(width - 2 * r, height)
  Shapes.Move(balloon[2], xb + r, yb)
  For i = 3 To 6
    balloon[i] = Shapes.AddEllipse(2 * r, 2 * r)
  EndFor
  Shapes.Move(balloon[3], xb, yb)
  Shapes.Move(balloon[4], xb + width - 2 * r, yb)
  Shapes.Move(balloon[5], xb, yb + height - 2 * r)
  Shapes.Move(balloon[6], xb + width - 2 * r, yb + height - 2 * r)
  root = Shapes.AddTriangle(50 + dx, yb + height, 65 + dx, yb + height, 65 + dx, yb + height + 20)
  GraphicsWindow.BrushColor = "#000000"
  GraphicsWindow.FontSize = 20
  talk = Shapes.AddText(txt)
  Shapes.Move(talk, xb + 10, yb + 10)
EndSub

Sub Game_HideBalloon
  If root <> "" Then
    Shapes.Remove(root)
    root = ""
  EndIf
  If talk <> "" Then
    Shapes.Remove(talk)
    talk = ""
  EndIf
  For i = 1 To 6
    If balloon[i] <> "" Then
      Shapes.Remove(balloon[i])
      balloon[i] = ""
    EndIf
  EndFor
EndSub

Sub OnMouseMove
  mouseMove = "True"
EndSub

Sub OnMouseDown
  mouseDown = "True"
EndSub

Sub Lang_GetTextFromDAT
  'Inicializar Idioma do Texto - Estes textos serão lidos do arquivo .dat.
  'Initialize Language Text - These text will be read from .dat file.
  fname[1] = "Diggory"
  fname[2] = "Johnny"
  fname[3] = "Lya"
  fname[4] = "Hana"
  Stack.PushValue("local", i)
  ln = 1
  datLine = File.ReadLine(Program.Directory + "/" + lang + ".dat", ln)
  While datLine <> ""
    nItem = Array.GetItemCount(datLine)
    index = Array.GetAllIndices(datLine)
    For i = 1 To nItem
      dat[index[i]] = datLine[index[i]]
    EndFor
    ln = ln + 1
    datLine = File.ReadLine(Program.Directory + "/" + lang + ".dat", ln)
  EndWhile
  nMenu = 0
  While Array.ContainsIndex(dat, "menu" + (nMenu + 1))
    nMenu = nMenu + 1
  EndWhile
  i = Stack.PopValue("local")
EndSub