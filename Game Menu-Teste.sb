'Game Menu 0.4
'Small Quest (Teste)
'Copyright © 2015 Filipe Cardoso Martins and Nonki Takahashi.  The MIT License.
'Last update 2015-06-04
'Program ID CBW762

'Configurações da janela do jogo. / The game window settings.
GraphicsWindow.Hide()
GraphicsWindow.Show()
GraphicsWindow.Title = "Game Menu 0.4"
GraphicsWindow.Height = "550" 
GraphicsWindow.Width = "800"

'Designer da janela do jogo. / Designer of the game window.
GraphicsWindow.BackgroundColor= "DodgerBlue"
GraphicsWindow.BrushColor= GraphicsWindow.GetColorFromRGB (255, 255, 255)
GraphicsWindow.FillRectangle (10, 10, 780, 530)
GraphicsWindow.BrushColor= GraphicsWindow.GetColorFromRGB (0, 0, 0)
GraphicsWindow.FillRectangle (15, 15, 770, 520)

'Foto de Fundo. / Photo Fund.
imagem = ImageList.LoadImage(Program.Directory + "/image/FundoTelaCarregamento.png")
GraphicsWindow.DrawImage(imagem, 15, 15)
GraphicsWindow.BrushColor = "#CC000000"
GraphicsWindow.FillRectangle(300, 160, 200, 260)

'Configurações do game menu. / Game settings menu.
GraphicsWindow.BrushColor = "DarkOrange"
GraphicsWindow.FontSize = "40"
nameGame = Shapes.AddText("Small Quest")
Shapes.Move(nameGame, 280, 50)
GraphicsWindow.BrushColor = "DeepSkyBlue"
GraphicsWindow.FontSize = "20"
menuName = Shapes.AddText("GAME MENU")
Shapes.Move(menuName, 337, 110)

GraphicsWindow.BrushColor = "White"
GraphicsWindow.PenColor = "Gray"
lang = "en-US"
GetTextFromDAT()
dark = 30
y0 = 180
dy = 35
For i = 1 To nMenu
  y = y0 + (i - 1) * dy
  menu[i] = Shapes.AddText(menuCaption[i])
  Shapes.Move(menu[i], menuX[i], y)
  Shapes.SetOpacity(menu[i], dark)
EndFor

GraphicsWindow.MouseMove = OnMouseMove
GraphicsWindow.MouseDown = OnMouseDown
inGame = "True"
While inGame
  If mouseMove Then
    my = GraphicsWindow.MouseY
    selected = ""
    For i = 1 To nMenu
      y1 = y0 + (i - 1) * dy
      y2 = y1 + dy
      If y1 <= my And my < y2 Then
        Shapes.SetOpacity(menu[i], 100)
        selected = i
      Else
        Shapes.SetOpacity(menu[i], dark)
      EndIf
    EndFor
    mouseMove = "False"
  EndIf
  If mouseDown Then
    If menuCaption[selected] = "EXIT" Then
      inGame = "False"
    EndIf
    mouseDown = "False"
  EndIf
EndWhile
Program.End()

Sub GetTextFromDAT
  ln = 1
  dat[ln] = File.ReadLine(Program.Directory + "/" + lang + ".dat", ln)
  While dat[ln] <> ""
    ln = ln + 1
    dat[ln] = File.ReadLine(Program.Directory + "/" + lang + ".dat", ln)
  EndWhile
  For i = 1 To ln
    If Text.StartsWith(dat[i], "menu1") Then
      menuCaption = dat[i]
      nMenu = Array.GetItemCount(menuCaption)
      For j = 1 To nMenu
        menuCaption[j] = menuCaption["menu" + j]
        menuCaption["menu" + j] = ""
      EndFor
    EndIf
    If Text.StartsWith(dat[i], "menuX1") Then
      menuX= dat[i]
      For j = 1 To nMenu
        menuX[j] = menuX["menuX" + j]
        menuX["menuX" + j] = ""
      EndFor
    EndIf
  EndFor
EndSub

Sub OnMouseMove
  mouseMove = "True"
EndSub

Sub OnMouseDown
  mouseDown = "True"
EndSub